*********************************************************************** 00049
*                                                                     * 00050
*  THIS PROGRAM IS A TSO COMMAND PROCESSOR THAT WILL LIST ON THE TER- * 00051
*  MINAL TO WHICH VOLUME A DATA SET IS CATALOGED AND ON WHICH         * 00052
*  ONLINE DIRECT ACCESS VOLUMES THE DATA SET CAN BE FOUND.            * 00053
*                                                                     * 00054
*           STOLEN AND MODIFIED BY BRIAN WESTERMAN 12/07/82           * 00055
*                                                                     * 00056
*********************************************************************** 00057
         EJECT                                                          00058
*********************************************************************** 00059
*  INITIALIZE.                                                        * 00060
*********************************************************************** 00061
LOCDSN   CSECT                                                          00062
START    EQU   *                                                        00063
         STM   R14,R12,12(R13)         SAVE REGISTERS                   00064
         LR    R12,R15                 LOAD BASE REGISTER               00065
         USING START,R12                                                00066
         LR    R2,R1                   SAVE CPPL PTR                    00067
         LA    R0,DEND-DSTART          GET SIZE OF DSECT                00068
         GETMAIN   R,LV=(R0)                                            00069
         LR    R11,R1                  LOAD DSECT BASE REGISTER         00070
         USING GOTTEN,R11                                               00071
         ST    R13,SAVE+4              ESTABLISH SAVE                   00072
         ST    R11,8(R13)                             AREA CHAINING     00073
         LR    R13,R11                 PTR TO MY SAVE AREA INTO R13     00074
         EJECT                                                          00075
*********************************************************************** 00076
*  SEARCH UCBS FOR DIRECT ACCESS DEVICES. BUILD A TABLE OF VOLUME     * 00077
*  SERIAL NUMBERS OF ALL ONLINE NON-VIO DIRECT ACCESS DEVICES.        * 00078
*********************************************************************** 00079
         XC    VOLCNT,VOLCNT           ZERO VOLUME COUNT                00080
         L     R4,16                   ADDRESS OF CVT INTO R4           00081
         L     R5,40(R4)               UCB LOOKUP LIST FROM CVT         00082
UCBSCAN  EQU   *                                                        00083
         LH    R6,0(R5)                GET ADDRESS OF A UCB             00084
         LTR   R6,R6                   TEST FOR VALID                   00085
         BZ    UCBINC                  NOT UCB ADDRESS                  00086
         BM    UCBEND                  END OF LIST                      00087
         CLI   2(R6),X'FF'             CHECK FOR UCB ID                 00088
         BNE   RETURN                  SOMETHING IS WRONG GET OUT       00089
         TM    18(R6),X'20'            IS IT DIRECT ACCESS              00090
         BNO   UCBINC                  NO                               00091
         TM    0(R6),X'80'             IS IT VIRTUAL DEVICE             00092
         BO    UCBINC                  YES-- DO NOT WANT VIRTUAL        00093
         TM    3(R6),X'80'             IS IT ONLINE                     00094
         BNO   UCBINC                  NO                               00095
         LA    R7,DASDLIST             GET START OF TABLE               00096
         LH    R8,VOLCNT               CURRENT VOLUME COUNT FOR LOOP    00097
         LTR   R8,R8                   TEST VOLUME COUNT                00098
         BZ    NEWVOL                  IF ZERO THIS IS FIRST VOLUME     00099
VOLSRCH  EQU   *                       LOOP THRU VOLUMES ALREADY FOUND  00100
         CLC   0(6,R7),28(R6)          TABLE ENTRY VS VOL IN UCB        00101
         BE    UCBINC                  VOLUME ALREADY IN TABLE          00102
         LA    R7,6(R7)                NEXT ENTRY IN TABLE              00103
         BCT   R8,VOLSRCH              IF NOT ALL CHECK NEXT            00104
NEWVOL   EQU   *                       NEW VOLUME FOUND                 00105
         MVC   0(6,R7),28(R6)          MOVE VOL FROM UCB TO TABLE       00106
         LH    R8,VOLCNT               GET VOLUME COUNT                 00107
         LA    R8,1(R8)                ADD 1                            00108
         STH   R8,VOLCNT               UPDATE                           00109
         CH    R8,=H'256'              CHECK FOR TABLE LIMIT            00110
         BE    UCBEND                  NO MORE ACCEPTED                 00111
UCBINC   EQU   *                                                        00112
         LA    R5,2(R5)                POINT TO NEXT UCB                00113
         B     UCBSCAN                 GO LOOK AT IT                    00114
UCBEND   EQU   *                                                        00115
         EJECT                                                          00116
*********************************************************************** 00117
*  TEST FOR COMMAND OPERANDS.  RETURN IF THERE ARE NONE.  CALCULATE   * 00118
*  THE ADDRESS AND LENGTH OF THE COMMAND OPERAND. CONVERT ANY LOWER   * 00119
*  CASE LETTERS TO UPPER CASE AND INITIALIZE DSN WORK AREA.           * 00120
*********************************************************************** 00121
         L     R3,12(R2)               ECT PTR INTO R3                  00122
         TM    28(R3),X'80'            ANY OPERANDS ?                   00123
         BO    NOOPERS                 BRANCH NO                        00124
         L     R3,0(R2)                CMD BUFFER PTR INTO R3           00125
         LH    R4,0(R3)                BUFFER LENGTH INTO R4            00126
         LA    R5,4                    SUBTRACT 4 TO ALLOW              00127
         SR    R4,R5                            FOR LENGTH WORD         00128
         SH    R4,2(R3)                SUBTRACT OFFSET TO ALLOW FOR CMD 00129
         LA    R5,4(R3)                PTR TO CMD INTO R5               00130
         AH    R5,2(R3)                PTR TO OPERAND INTO R5           00131
         LR    R3,R5                   PTR TO OPERAND INTO R3           00132
         LR    R5,R4                   SAVE DSN LENGTH IN R5            00133
         BCTR  R5,0                    REDUCE BY 1 FOR EXECUTE INST     00134
         EX    R5,LOWTOUP              CONVERT LOWER CASE TO UPPER      00135
         LA    R5,44                   SET DSNLEN                       00136
         STH   R5,DSNLEN                          TO 44                 00137
         MVI   DSN,C' '                BLANK OUT                        00138
         MVC   DSN+1(L43),DSN                    DSN AREA               00139
         EJECT                                                          00140
*********************************************************************** 00141
*  CHECK TO SEE WHAT THE USER WANTS.  IF ALL HE WANTS IS A            * 00142
*  PARTICULAR DATA SET, WRITE THE DSN ON THE TERMINAL AND GO CHECK    * 00143
*  TO SEE IF LOGON ID NEEDS TO BE ADDED TO THE USER SUPPLIED DATA SET * 00144
*  NAME.                                                              * 00145
*********************************************************************** 00146
         LR    R1,R3                   PTR TO DSN INTO R1               00147
         LR    R0,R4                   DSN LENGTH INTO R0               00148
         BAL   R10,TPUT                WRITE DSN TO TERMINAL            00149
*********************************************************************** 00150
*  CHECK TO SEE IF USER'S LOGON ID MUST BE APPENDED TO THE SUPPLIED   * 00151
*  DSNAME AND DO SO IF IT IS NECESSARY.  MOVE THE FULLY QUALIFIED     * 00152
*  DSNAME TO A CONTROL AREA IN PREPARATION FOR ISSUING THE LOCATE     * 00153
*  MACRO.                                                             * 00154
*********************************************************************** 00155
CHEKQUOT LA    R6,44                   DSN AREA LENGTH INTO R6          00156
         LA    R5,DSN                  PTR TO DSN AREA INTO R5          00157
         CLI   0(R3),C''''             IS 1ST CHARACTER A QUOTE ?       00158
         BE    SKIPQUOT                BRANCH YES                       00159
         L     R7,8(R2)                PSCB PTR INTO R7                 00160
         MVC   DSN(L7),0(R7)           MOVE USER ID TO DSN AREA         00161
ISITBLK  CLI   0(R5),C' '              IS THIS CHARACTER A BLANK ?      00162
         BE    MOVEDOT                 BRANCH YES                       00163
         LA    R5,1(R5)                BUMP OVER 1 CHARACTER            00164
         BCT   R6,ISITBLK              REDUCE LENGTH BY 1 & LOOP        00165
MOVEDOT  MVI   0(R5),C'.'              MOVE PERIOD AFTER USER ID        00166
         LA    R5,1(R5)                BUMP OVER 1 CHARACTER            00167
         BCT   R6,MOVEDSN              REDUCE LENGTH BY 1 & BRANCH      00168
SKIPQUOT LA    R3,1(R3)                BUMP OVER 1 CHARACTER IN OPERAND 00169
         BCTR  R4,0                    REDUCE OPERAND LENGTH BY 1       00170
MOVEDSN  LTR   R4,R4                   IS THIS THE END OF THE OPERAND ? 00171
         BZ    LOCATE                  BRANCH YES                       00172
         CLI   0(R3),C''''             IS THIS CHARACTER A QUOTE ?      00173
         BE    LOCATE                  BRANCH YES                       00174
         MVC   0(L1,R5),0(R3)          MOVE 1 CHARACTER OF DSN OPERAND  00175
         LA    R3,1(R3)                BUMP OVER 1 CHARACTER IN OPERAND 00176
         LA    R5,1(R5)                BUMP OVER 1 CHARACTER IN DSN     00177
         BCTR  R4,0                    REDUCE OPERAND LENGTH BY 1       00178
         BCT   R6,MOVEDSN              REDUCE DSN LENGTH BY 1 AND LOOP  00179
         EJECT                                                          00180
*********************************************************************** 00181
*  ISSUE THE LOCATE MACRO TO FIND THE VOLUME ON WHICH THE DATA SET    * 00182
*  RESIDES.  IF THE DATA SET IS NOT CATALOGGED ISSUE AN INFORMATIVE   * 00183
*  MESSAGE TO THE TERMINAL. IF THE DATA SET SPANS MORE THAN ONE     *   00184
*  VOLUME ISSUE A MESSAGE INDICATING THAT THE DATA SET CANNOT BE PRO- * 00185
*  CESSED AND RETURN.  IF LOCATE IS SUCCESSFUL CONTINUE PROCESSING.   * 00186
*********************************************************************** 00187
LOCATE   LA    R3,0                    ZERO R3                          00188
         ST    R3,CAMLIST              ZERO WORD 1 OF CAMLIST           00189
         MVI   CAMLIST,X'44'           INDICATE LOCATE OPTION           00190
         ST    R3,CAMLIST+8            ZERO WORD 3 OF CAMLIST           00191
         LA    R3,DSN                  DSN PTR INTO                     00192
         ST    R3,CAMLIST+4                         WORD 2 OF CAMLIST   00193
         LA    R3,SERAREA              SER PTR INTO                     00194
         ST    R3,CAMLIST+12                        WORD 4 OF CAMLIST   00195
         LOCATE  CAMLIST               FIND DSN IN CATALOG              00196
         LTR   R15,R15                 LOCATE SUCCESSFUL ?              00197
         BNZ   NOTCATLG                BRANCH NO                        00198
         LA    R3,1                    DOES DATA SET SPAN MORE          00199
         CH    R3,SERAREA                        THAN 1 VOLUME ?        00200
         BNE   MORTHAN1                BRANCH YES                       00201
         EJECT                                                          00202
*********************************************************************** 00203
*   ENTRY AT LOCATE2 PUTS OUT CATLOG MESSAGE.                         * 00204
*   ENTRY AT SETLOOP OR DROP THRU SETS UP VOLUME LOOP FOR OBTAIN      * 00205
*********************************************************************** 00206
LOCATE2  EQU   *                                                        00207
         LH    R0,MSG2                 SET MESSAGE LENGTH               00208
         MVC   OUTLINE(28),MSG2+2      MOVE TO GOTTEN AREA              00209
         MVC   OUTLINE+22(6),SERAREA+6    MOVE IN VOL SER               00210
         LA    R1,OUTLINE              ADDRESS OF MESSAGE               00211
         BAL   R10,TPUT                SEND CATLOGED MESSAGE            00212
SETLOOP  EQU   *                                                        00213
         LH    R0,MSG7                 SET MESSAGE LENGTH               00214
         LA    R1,MSG7+2               ADDRESS OF MESSAGE               00215
         BAL   R10,TPUT                SEND HEADER MESSAGE              00216
         LH    R8,VOLCNT               SET UP LOOP CONTROL              00217
         LA    R7,DASDLIST             POINT TO FIRST IN TABLE          00218
         EJECT                                                          00219
*********************************************************************** 00220
*  OBTAIN THE FORMAT 1 DSCB FOR THE DATA SET.  IF THE OBTAIN IS NOT   * 00221
*  SUCCESSFUL GO ON TO NEXT VOLUME ELSE DISPLAY VOLUME ON SCREEN      * 00222
*********************************************************************** 00223
OBTAIN1  LA    R3,0                    ZERO R3                          00224
         ST    R3,CAMLIST              ZERO WORD 1 OF CAMLIST           00225
         MVI   CAMLIST,X'C1'           INDICATE SEARCH OPTION OF OBTAIN 00226
         LA    R3,DSN                  DSN PTR INTO                     00227
         ST    R3,CAMLIST+4                         WORD 2 OF CAMLIST   00228
*        LA    R3,SERAREA+6            SER PTR INTO                     00229
         ST    R7,CAMLIST+8                         WORD 3 OF CAMLIST   00230
         LA    R3,OBTAINWK             WORK PTR INTO                    00231
         ST    R3,CAMLIST+12                         WORD 4 OF CAMLIST  00232
         OBTAIN  CAMLIST               OBTAIN FORMAT 1 DSCB             00233
         LTR   R15,R15                 OBTAIN SUCCESSFUL ?              00234
         BNZ   LOOPCNTL                BRANCH NO                        00235
         MVI   OUTLINE,X'40'           BLANK OUT MSG AREA               00236
         MVC   OUTLINE+1(13),OUTLINE                                    00237
         MVC   OUTLINE+4(6),0(R7)      PUT IN VOLSER                    00238
         LA    R0,14                   LOAD LENGTH OF MESSAGE           00239
         LA    R1,OUTLINE              ADDRESS OF MESSAGE               00240
         BAL   R10,TPUT                SEND MESSAGE                     00241
LOOPCNTL EQU   *                                                        00242
         LA    R7,6(R7)                NEXT ENTRY IN TABLE              00243
         BCT   R8,OBTAIN1              IF MORE VOLUMES GO BACK          00244
         B     RETURN                  FINISHED                         00245
         EJECT                                                          00246
*********************************************************************** 00247
*  RETURN TO THE SYSTEM AFTER FREEING WORK AREA                       * 00248
*********************************************************************** 00249
RETURN   EQU   *                                                        00250
         LR    R1,R13                  PTR TO GOTTEN AREA INTO R1       00251
         L     R13,SAVE+4              RESTORE R13                      00252
         LA    R0,DEND-DSTART          GET SIZE OF DSECT                00253
         FREEMAIN  R,LV=(R0),A=(R1)                                     00254
         LM    R14,R12,12(R13)         RESTORE R14-R12                  00255
         SR    R15,R15                 ZERO R15 FOR RETURN CODE         00256
         BR    R14                     RETURN TO INVOKER                00257
         EJECT                                                          00258
*********************************************************************** 00259
*  ROUTINE TO WRITE LINES TO THE TERMINAL.                            * 00260
*********************************************************************** 00261
TPUT     TPUT  (R1),(R0),R             WRITE MSG TO TERMINAL            00262
         BR    R10                     RETURN TO INVOKER                00263
         EJECT                                                          00264
*********************************************************************** 00265
*  ROUTINES TO ISSUE ERROR MESSAGES.                                  * 00266
*********************************************************************** 00267
NOTCATLG LH    R0,MSG1                 MSG LENGTH INTO R0               00268
         LA    R1,MSG1+2               PTR TO MSG INTO R1               00269
         BAL   R10,TPUT                WRITE MSG TO TERMINAL            00270
         B     SETLOOP                 GO SEARCH VOLUMES                00271
MORTHAN1 LH    R0,MSG3                 MSG LENGTH INTO R0               00272
         LA    R1,MSG3+2               PTR TO MSG INTO R1               00273
         BAL   R10,TPUT                WRITE MSG TO TERMINAL            00274
         B     RETURN                  GO TO RETURN                     00275
BADCATRD LH    R0,MSG4                 MSG LENGTH INTO R0               00276
         LA    R1,MSG4+2               PTR TO MSG INTO R1               00277
         BAL   R10,TPUT                WRITE MSG TO TERMINAL            00278
         B     RETURN                  GO TO RETURN                     00279
UNUSDIND LH    R0,MSG5                 MSG LENGTH INTO R0               00280
         LA    R1,MSG5+2               PTR TO MSG INTO R1               00281
         BAL   R10,TPUT                WRITE MSG TO TERMINAL            00282
         B     RETURN                  GO TO RETURN                     00283
NOOPERS  LH    R0,MSG6                 MSG LENGTH INTO R0               00284
         LA    R1,MSG6+2               PTR TO MSG INTO R1               00285
         BAL   R10,TPUT                WRITE MSG TO TERMINAL            00286
         B     RETURN                  GO TO RETURN                     00287
         EJECT                                                          00288
*********************************************************************** 00289
*  INSTRUCTION THAT WHEN EXECUTED WILL CONVERT LOWER CASE LETTERS TO  * 00290
*  UPPER CASE.                                                        * 00291
*********************************************************************** 00292
LOWTOUP  OC    0(L0,R3),BLANKS         OBJECT OF EXECUTE INSTRUCTION    00293
         EJECT                                                          00294
*********************************************************************** 00295
*  CONSTANTS.                                                         * 00296
*********************************************************************** 00297
MSG1     DC    H'29'                                                    00298
         DC    CL29'DATA SET NOT FOUND IN CATALOG'                      00299
MSG2     DC    H'28'                                                    00300
         DC    CL28'DATA SET CATALOGED TO XXXXXX'                       00301
MSG3     DC    H'53'                                                    00302
         DC    CL53'DATA SET SPANS MORE THAN 1 VOLUME - UNABLE TO PROCE+00303
               SS'                                                      00304
MSG4     DC    H'21'                                                    00305
         DC    CL21'ERROR IN CATALOG READ'                              00306
MSG5     DC    H'12'                                                    00307
         DC    CL12'UNUSED INDEX'                                       00308
MSG6     DC    H'40'                                                    00309
         DC    CL40'DATA SET NAME REQUIRED. PLEASE TRY AGAIN'           00310
MSG7     DC    H'35'                                                    00311
         DC    CL35'DATA SET FOUND ON FOLLOWING VOLUMES'                00312
BLANKS   DC    CL80' '                                                  00313
         EJECT                                                          00314
*********************************************************************** 00315
*  DSECT FOR GOTTEN STORAGE.                                          * 00316
*********************************************************************** 00317
GOTTEN   DSECT                                                          00318
DSTART   EQU   *                                                        00319
SAVE     DS    18F                                                      00320
PARMWD1  DS    F                                                        00321
PARMWD2  DS    F                                                        00322
         DS    0H                                                       00323
OUTLINE  DS    CL57                                                     00324
         DS    0D                                                       00325
SERAREA  DS    265C                                                     00326
         ORG   *-249                                                    00327
OBTAINWK DS    148C                                                     00328
F3CCHHR  DS    CL5                                                      00329
SERENDUP DS    96C                                                      00330
DSNLEN   DS    H                                                        00331
DSN      DS    CL44                                                     00332
CAMLIST  DS    4F                                                       00333
VOLCNT   DS    H                                                        00334
DASDLIST DS    256CL6                                                   00335
DEND     EQU   *                                                        00336
         EJECT                                                          00337
*********************************************************************** 00338
*  EQUATES FOR LENGTH VALUES AND GENERAL REGISTERS.                   * 00339
*********************************************************************** 00340
L0       EQU   0                                                        00341
L1       EQU   1                                                        00342
L2       EQU   2                                                        00343
L3       EQU   3                                                        00344
L4       EQU   4                                                        00345
L5       EQU   5                                                        00346
L6       EQU   6                                                        00347
L7       EQU   7                                                        00348
L9       EQU   9                                                        00349
L20      EQU   20                                                       00350
L43      EQU   43                                                       00351
R0       EQU   0                                                        00352
R1       EQU   1                                                        00353
R2       EQU   2                                                        00354
R3       EQU   3                                                        00355
R4       EQU   4                                                        00356
R5       EQU   5                                                        00357
R6       EQU   6                                                        00358
R7       EQU   7                                                        00359
R8       EQU   8                                                        00360
R9       EQU   9                                                        00361
R10      EQU   10                                                       00362
R11      EQU   11                                                       00363
R12      EQU   12                                                       00364
R13      EQU   13                                                       00365
R14      EQU   14                                                       00366
R15      EQU   15                                                       00367
         END                                                            00368
