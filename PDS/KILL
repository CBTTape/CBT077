         MACRO
&LABEL   TPUTR &MSG,&LEN
&LABEL   LA    R1,&MSG
         LA    R0,&LEN
         BAL   R14,TPUT
         MEND
         SPACE 3
         PRINT NOGEN
         CVT   DSECT=YES
         IHAASVT
         IHAASCB
         IHAPSA
         PRINT GEN
         EJECT
$KILL    CSECT
         STM   14,12,12(13)
         LR    10,15              SETUP BASE REGISTER
         USING $KILL,10
         ST    13,SAVE+4          BACKCHAIN
         LR    12,13
         LA    13,SAVE
         ST    13,8(12)           FRONTCHAIN
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
         EJECT
         TPUTR HEADER,L'HEADER
         TPUTR SPACES,1
         L     R3,CVTPTR
         L     R3,CVTASVT-CVT(R3)
         L     R4,ASVTMAXU-ASVT(R3)
         LA    R5,ASVTENTY-ASVT(R3)
         SLR   R9,R9
         SPACE 3
LOOP     DS    0H             SHOW ASID#S FOR ALL ACTIVE TASKS
         LA    R9,1(R9)
         L     R6,0(R5)
         LTR   R6,R6
         BM    ASIDINCR
         CH    R9,ASCBASID-ASCB(R6)
         BNE   MISMATCH
         LM    R7,R8,ASCBJBNI-ASCB(R6)
         LTR   R7,R7
         BZ    *+10
         MVC   TPUTJBN,0(R7)       INITIATED JOB NAME
         LTR   R8,R8
         BZ    *+10
         MVC   TPUTSMLN,0(R8)      START/MOUNT/LOGON ID
         CVD   R9,DBLWORD
         UNPK  TPUTASID,DBLWORD
         OI    TPUTASID+L'TPUTASID-1,X'F0'
         TPUTR ASIDPRT,L'ASIDPRT
         MVC   ASIDPRT,ASIDPRT-1
ASIDINCR DS    0H
         LA    R5,4(R5)
         BCT   R4,LOOP
         SPACE 3
         TPUTR SPACES,1
         TCLEARQ INPUT
         TPUTR REQ1MSG,L'REQ1MSG
         TGET  ANSAREA,L'ANSAREA
         LTR   R15,R15
         BNZ   OUT
         AH    R1,=H'-1'
         BM    OUT
         EX    R1,CHKASID
         BNZ   OUT
         EX    R1,PACKASID
         UNPK  REQ2ASID,DBLWORD
         OI    REQ2ASID+L'REQ2ASID-1,X'F0'
         CVB   R2,DBLWORD
         CH    R2,=H'2'       DONT LET USER KILL ASM OR MASTER A/S
         BNH   OUT
         C     R2,ASVTMAXU-ASVT(R3)
         BH    OUT
         LR    R5,R2          *** ASID TO KILL (MAYBE) ***
         SLL   R2,2
         L     R2,ASVTENTY-ASVT-4(R3,R2)
         LTR   R2,R2          ADDRSPC ASSIGNED?
         BNP   OUT
         CH    R5,ASCBASID-ASCB(R2)
         BNE   MISMATCH
         LM    R1,R2,ASCBJBNI-ASCB(R2)
         LTR   R1,R1
         BZ    *+10
         MVC   REQ2JBN,0(R1)
         LTR   R2,R2
         BZ    *+10
         MVC   REQ2SMLN,0(R2)
         TCLEARQ INPUT
         TPUTR REQ2MSG,L'REQ2MSG
         MVC   ANSAREA,ANSAREA-1
         TGET  ANSAREA,L'ANSAREA
         LTR   R15,R15
         BNZ   OUT
         CLC   ANSAREA,CONTANSR
         BNE   OUT
         SPACE 3
         LA    R0,0
         LA    R1,1
         SVC   244
         MODESET KEY=ZERO,MODE=SUP
         ST    R13,R13SAVE
         LA    R13,RTMSAVE
         CALLRTM TYPE=MEMTERM,ASID=(R5),COMPCOD=(R5)
         L     R13,R13SAVE
         MODESET KEY=NZERO,MODE=PROB
         LA    R1,0
         SVC   244
         TPUTR DONEMSG,L'DONEMSG
         B     GOBACK
         SPACE 3
OUT      TPUTR ERRMSG,L'ERRMSG
         SPACE 1
GOBACK   L     13,SAVE+4
         SR    R15,R15
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
         SPACE 3
TPUT     DS    0H
         TPUT  (1),(0)
         BR    R14
         SPACE 3
MISMATCH TPUTR MISMMSG,L'MISMMSG
         B     GOBACK
         EJECT
PACKASID PACK  DBLWORD,ANSAREA(1)
CHKASID  TRT   ANSAREA(1),NUMERIC
         SPACE 2
DBLWORD  DS    D
NUMERIC  DC    256XL1'FF'
         ORG   NUMERIC+C'0'
         DC    10XL1'0'
         ORG   NUMERIC+256-4-18*4
R13SAVE  DS    F
SAVE     DS    18F
RTMSAVE  DS    18F
         DC    CL1' '
ANSAREA  DC    CL12' '
CONTANSR DC    CL(L'ANSAREA)'PEPPER'
SPACES   DC    CL1' '
ASIDPRT  DC    C'00000                  '
TPUTASID EQU   ASIDPRT,5
TPUTJBN  EQU   ASIDPRT+6,8
TPUTSMLN EQU   ASIDPRT+15,8
REQ1MSG  DC    C'SPECIFY ASID# OF ADDRSPC TO BE TERMINATED'
REQ2MSG  DC    C'VERIFY ASID XXXXX NAME ++++++++ ++++++++ FOR MEMTERM'
REQ2ASID EQU   REQ2MSG+12,5
REQ2JBN  EQU   REQ2MSG+23,8
REQ2SMLN EQU   REQ2MSG+32,8
HEADER   DC    C'ASID# JOBNAME  STCNAME'
DONEMSG  DC    C'''$KILL'' PROCESSING COMPLETE'
ERRMSG   DC    C'''$KILL'' TERMINATED BEFORE INVOKING RTM'
         LTORG
MISMMSG  DC    C'''ASCBASID'' DOES NOT MATCH POSITION IN ''ASVT'''
         END
