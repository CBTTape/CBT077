SHOWDATE TITLE 'ROUTINE TO PUT DATE/TIME ON OPERATOR CONSOLE'           00001
*                                                                       00002
R0       EQU   0                                                        00003
R1       EQU   1                                                        00004
R2       EQU   2                                                        00005
R3       EQU   3                                                        00006
R4       EQU   4                                                        00007
R5       EQU   5                                                        00008
R6       EQU   6                                                        00009
R7       EQU   7                                                        00010
R8       EQU   8                                                        00011
R9       EQU   9                                                        00012
R10      EQU   10                                                       00013
R11      EQU   11                                                       00014
R12      EQU   12                                                       00015
R13      EQU   13                                                       00016
R14      EQU   14                                                       00017
R15      EQU   15                                                       00018
*                                                                       00019
SHOWDATE SAVE (14,12),,*                                                00020
         LR    R12,R15                 SETUP BASE REG                   00021
         USING SHOWDATE,R12                                             00022
         LA    R15,SAVEAREA            INIT SAVE AREA                   00023
         ST    R15,8(R13)                                               00024
         ST    R13,4(R15)                                               00025
         LR    R13,R15                                                  00026
         TIME  DEC                                                      00027
         ST    R1,DATE                 SAVE DATE FOR LATER              00028
***FORMAT THE PRESENT TIME (AS SET BY THE OPERATOR)                     00029
         ST    R0,DBLWORK              CNVT PACKED TO DEC               00030
         MVI   DBLWORK+4,X'0F'                                          00031
         UNPK  WORKWORD(9),DBLWORK(5)                                   00032
         MVC   TIMEHRS,WORKWORD        MOVE TIME TO MSG                 00033
         MVC   TIMMINS,WORKWORD+2                                       00034
         MVC   TIMSECS,WORKWORD+4                                       00035
         MVC   TIMHUNS,WORKWORD+6                                       00036
***FORMAT TODAY'S DATE INTO THE OUTPUT MSG                              00037
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.          00038
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS. 00039
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.                     00040
         XC    DBLWORK,DBLWORK                                          00041
         MVC   DBLWORK+4(4),WORKWORD   CNVT YEAR TO BIN                 00042
         CVB   R0,DBLWORK                                               00043
         ST    R0,BINYEAR              AND SAVE                         00044
         TM    BINYEAR+3,X'03'         LEAP YEAR?                       00045
         BNZ   MON1                    NO.                              00046
         MVI   MONMSK+10,29            YES, CORRECT                     00047
MON1     XC    DBLWORK,DBLWORK         CNVT DAYS TO BIN                 00048
         MVC   DBLWORK+6(2),DATE+2                                      00049
         CVB   R7,DBLWORK                                               00050
         ST    R7,BINDAYS              SAVE FOR LATER                   00051
         XR    R1,R1                                                    00052
         LA    R15,11                                                   00053
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY                00054
         SR    R7,R1                                                    00055
         BNP   MONOVR                                                   00056
         BCT   R15,MON2                                                 00057
         B     *+6                                                      00058
MONOVR   AR    R7,R1                   CORRECT OVERDRAW                 00059
         CVD   R7,DBLWORK              FORMAT DAY                       00060
         UNPK  CDAYN(2),DBLWORK+6(2)                                    00061
         OI    CDAYN+1,X'F0'           FIX UP SIGN                      00062
         MH    R15,=H'3'               GET TABLE OFFSET                 00063
         LA    R15,MONTAB(R15)         AND POINT AT MONTH               00064
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG               00065
***DAY OF WEEK                                                          00066
         L     R15,BINYEAR                                              00067
         SH    R15,=H'69'                                               00068
         BNP   OUTT                    MUST BE GREATER OR BAD           00069
         XR    R14,R14                 CLEAR DAY REG                    00070
         D     R14,=F'4'                                                00071
         MH    R15,=H'5'                                                00072
         AR    R14,R15                                                  00073
         A     R14,BINDAYS                                              00074
         SRDA  R14,32                                                   00075
         D     R14,=F'7'               MODULO 7 FOR WEEK                00076
         MH    R14,=H'3'                                                00077
         LA    R14,DAYTAB(R14)         POINT AT TODAY                   00078
         MVC   CDAY(3),0(R14)          INSERT IN MSG                    00079
***                                                                     00080
OUTT     MVC   M1DATE(LEN),TIMEHRS     COPY TIME & DATE TO USER'S AREA  00081
         MVC   M1JDATE,=X'4020204B202020'                               00082
         ED    M1JDATE,DATE+1                                           00083
*                                                                       00084
DOWTO    WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSG0)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSG1)                                              00085
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSG0)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         WTO   MF=(E,MSGBLNK)
         L     R13,4(R13)              RESTORE REGS & RETURN            00086
         RETURN (14,12),T,RC=0                                          00087
 EJECT                                                                  00088
DBLWORK  DC    D'0'                                                     00089
SAVEAREA DC    18F'0'                                                   00090
BINYEAR  DC    F'0'                                                     00091
BINDAYS  DC    F'0'                                                     00092
DATE     DC    F'0'                                                     00093
WORKWORD DC    2F'0'                                                    00094
         LTORG                                                          00095
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)                 00096
TIMEHRS  DC    CL2' ',C'.'                                              00097
TIMMINS  DC    CL2' ',C'.'                                              00098
TIMSECS  DC    CL2' ',C'.'                                              00099
TIMHUNS  DC    CL2' ',C' '                                              00100
CDAY     DC    CL3' ',C' '                                              00101
CMON     DC    CL3' ',C' '                                              00102
CDAYN    DC    CL2' ',C',20'                                            00103
CYR      DC    CL2'  ',C' '                                             00104
LEN      EQU   *-TIMEHRS                                                00105
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'                                 00106
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'                  00107
MSGBLNK  WTO   '*                                                      X00108
                                   ',ROUTCDE=(11,13),MF=L               00109
MSG0     WTO   'BII428 *************** DATE ***************            X00108
                                   ',ROUTCDE=(11,13),MF=L               00109
MSG1     WTO   'BII429 YY.DDD  HH.MM.SS.TH DAY MON DD,19XX             X00108
                                   ',ROUTCDE=(11,13),MF=L               00109
         ORG   MSG1+10                                                  00110
M1JDATE  DS    CL7                                                      00111
         ORG   MSG1+19                                                  00112
M1DATE   DS    CL28                                                     00113
         ORG                                                            00114
         END                                                            00115
